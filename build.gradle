apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

version = '1.3'
group = 'me.nallar'
archivesBaseName = 'JavaPatcher'

// Extended project information
ext.projectName = 'JavaPatcher'
ext.inceptionYear = '2014'
ext.packaging = 'jar'
ext.url = 'http://nallar.me/minecraft'
ext.description = 'Java Patcher'
ext.organization = 'nallar.me'

// Define variables
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

// Source compiler configuration
tasks.withType(JavaCompile) {
	sourceCompatibility = 7
	targetCompatibility = 7
	options.with {
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << '-Xlint:-path' << '-Xlint:-classfile' << '-Xlint:-processing'
	}
}

repositories {
	mavenCentral()
}

configurations {
	provided
	compile.extendsFrom provided
}

dependencies {
	provided 'org.apache.logging.log4j:log4j-core:2.0.2'
	provided 'com.google.guava:guava:18.0'
	provided 'org.javassist:javassist:3.20.0-GA'
	provided 'org.projectlombok:lombok:1.16.4'
	compile 'org.json:json:20090211'
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from((configurations.runtime - configurations.provided).filter { it.exists() }.collect {
		it.isDirectory() ? it : zipTree(it)
	}) {
		exclude "META-INF/**"
	}
}

jar.manifest.mainAttributes(
	"Built-By": System.properties['user.name'],
	"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
	"Implementation-Title": name,
	"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
	"Implementation-Vendor": url)
